if (PLAN_IS_APPLICATION) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

def cfg    = rootProject.ext.android
def appId  = rootProject.ext.appId
def java_8 = JavaVersion.VERSION_1_8

android {
    compileSdkVersion cfg.COMLIER_SDK_VERSION
    buildToolsVersion cfg.BUILD_TOOLS_VERSION


    defaultConfig {
        if (PLAN_IS_APPLICATION) {
            applicationId appId["plan"]
        }
        minSdkVersion cfg.MIN_SDK_VERSION
        targetSdkVersion cfg.TAR_SDK_VERSION
        versionCode cfg.VERSION_CODE
        versionName cfg.VERSION_NAME
        testInstrumentationRunner cfg.testInstrumentationRunner

        consumerProguardFiles 'consumer-rules.pro'

        // ARouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility java_8
        targetCompatibility java_8
    }

    // 配置 Manifest
    sourceSets {
        main {
            if (PLAN_IS_APPLICATION) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // ARouter
    implementation 'com.alibaba:arouter-api:1.5.0'
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'

    implementation project(path: ':Provider')
}
